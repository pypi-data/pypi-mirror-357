
?start: statement*

?statement: sum
      | NAME "=" sum -> assign_var
      | "if" "(" condition ")" block -> if_block
      | "repeat" "(" sum ")" block -> repeat_block
      | "for" "(" sum "in" sum ")" block -> for_block
      | "func" NAME "(" params? ")" block -> func_block
      | "import" /(.+?)(?=\.[Ll][Mm][Pp])/ import_args? -> import_stmt

?import_args: ".lmp" "(" NAME ("," NAME)* ")" -> import
            | "(" NAME ("," NAME)* ")" -> import
            | ".lmp" 


?bool: "true" -> true
     | "false" -> false

?condition: sum "==" sum  -> eq
          | sum "!=" sum  -> ne
          | sum "<" sum  -> lt
          | sum "<=" sum  -> le
          | sum ">" sum  -> gt
          | sum ">=" sum  -> ge
          | bool

?block: "{" code "}"

?code: sum
     | code sum -> add_code

?func: "out" "(" sum ")" -> out
     | "sqrt" "(" sum ")" -> sqrt
     | "pow" "(" sum "," sum ")" -> pow
     | NAME "(" args? ")" -> default_func

?args: sum ("," sum)* -> args

?params: NAME ("," NAME)* -> params

?sum: product
    | sum "+" product -> add
    | sum "-" product -> sub

?product: atom
    | product "*" atom -> mul
    | product "/" atom -> div
    | product "%" atom -> mod

?list: "[" items "]"
     | "[" "]" -> empty_list
     | "[" item "]" -> single_list

?items: item
      | items "," item -> add_item

?item: atom

?dict: "{" dict_items "}"
      | "{" "}" -> empty_dict

?dict_items: dict_item
           | dict_items "," dict_item

?dict_item: sum ":" sum -> dict_pair

?atom: NUMBER           -> number
     | STRING           -> str
     | list
     | dict             -> dict_val
     | bool
     | "-" atom         -> neg
     | NAME             -> var
     | "(" sum ")"
     | func

STRING: /"([^"]*)"/

%import common.CNAME -> NAME
%import common.NUMBER
%import common.WS

%ignore WS
%ignore /\/\/[^\n]*/